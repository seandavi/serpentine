__author__  = "Johannes Koester (http://johanneskoester.bitbucket.org)"
__license__ = "MIT"

### bwa.rules
rule mergeBam2Sample:
    params:
        batch="-l nodes=1:gpfs"
    input: lambda wildcards: expand("TEMP/{unit}.unit.bam", \
                             unit = SAMPLE_TO_UNIT[wildcards.sample])
    output:
        "TEMP/{sample}.sample.bam"
    threads: 2
    version: "1.119"
    run:
        cmd = """module load picard/1.119 && java -jar $PICARDJARPATH/MergeSamFiles.jar AS=true USE_THREADING=true VALIDATION_STRINGENCY=SILENT {0}""".format(' '.join(['I={0}'.format(i) for i in input]))
        cmd = cmd + " O={output}"
        print(cmd)
        shell(cmd,shell=True)

rule bwamem_map:
    """Map individual units with bwa mem

    This rule uses the reference genome, after index creation,
    and maps the reads. Read group info is added automatically
    (LB, SM, PL, and ID).  Output is a coordinate-sorted BAM file.
    The output file is marked as temp().
    """
    input:
        index=lambda wildcards: "resources/mapping/bwaindex_"+ config['bwaVersion'] +"/"+config['reference']+".pac",
        fastq=lambda wildcards: config["units"][wildcards.unit]
    output:
        "TEMP/{unit}.unit.bam"
    version: config['bwaVersion']
    params:
        batch ="-l nodes=1:gpfs -q ccr",
        sample =lambda wildcards: UNIT_TO_SAMPLE[wildcards.unit],
        library=lambda wildcards: UNIT_TO_LIBRARY[wildcards.unit],
        platform=config.get("platform","Illumina"),
        bwa_index = lambda wildcards: "resources/mapping/bwaindex_" + config['bwaVersion'] + "/" + config['reference'],
        output_prefix = "TEMP/{unit}.unit",
        custom=config.get("params_bwa_mem", "")
    log: "TEMP/{unit}.unit.log"
    threads: 64
    shell: "module load bwa/0.7.10 && "
        "bwa mem {params.custom} "
        r"-R '@RG\tID:{wildcards.unit}\t"
        r"SM:{params.sample}\tLB:{params.library}\tPL:{params.platform}' "
        "-t {threads} {params.bwa_index} {input.fastq}  2> {log} "
        "| samtools view -Sbh - "
        "| samtools sort -m 30000000000 - {params.output_prefix}"

rule bwa_index_fasta:
    """Index fasta input for bwa.

    This rule uses config['references'] to get a specific fasta 
    file for indexing. The index is created prior to mapping anything.
    """
    input:
        lambda wildcards: config['references'][wildcards.reference]
    output: "resources/mapping/bwaindex_" + config['bwaVersion'] + "/{reference}.pac"
    log: "resources/mapping/bwaindex_" + config['bwaVersion'] + "/{reference}.log"
    version: config['bwaVersion']
    params: batch='-l nodes=1:gpfs',
        prefix="resources/mapping/bwaindex_" + config['bwaVersion'] + "/{reference}"
    shell: "module load bwa/0.7.10 && bwa index -p {params.prefix} {input} 2> {log}"
