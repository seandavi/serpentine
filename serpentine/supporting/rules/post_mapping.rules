__author__  = "Sean Davis & Jack Zhu"
__license__ = "MIT"

def _get_unit_bams_from_sample(wc):
    ret = []
    for u in SAMPLE_TO_UNIT[wc.sample]:
        ret += ["RESULTS/{reference}.{aligner}/{sample}/{unit}.unit.bam".format(unit=u,
                                                                                                      sample=wc.sample,
                                                                                                      reference=wc.reference,
                                                                                                      aligner=wc.aligner)]
    return(ret)

def _knownIndels_to_GATK(known):
    """Convert a list of file locations to a format for the GATK command line"""
    try:
        return(' '.join(["-known " + x for x in known]))
    except KeyError:
        return(' ')

rule bamtdf:
    input:
        bam="{base}.bam",
        bai="{base}.bam.bai",
    output: 
        "{base}.bam.tdf"
    params: 
        batch="-l nodes=1:gpfs -q ccr",
        genome = config['reference'].replace("human_g1k_v37", "1kg_v37")
    version: 
        "2.3.31"
    shell: """
module load igvtools/{version}
${{IGVTOOLSHOME}}/igvtools \
count {input.bam} {output} {params.genome}
"""

rule gatk_print_reads:
    input:
        bam = "{base}.bam",
        bai = "{base}.bam.bai",
        recal_table = "{base}.bam.recal_table",
        reference = config['references'][config['reference']]
    output:
        "{base}.recal.bam"
    params:
        batch = "-l nodes=1:gpfs -q ccr"
    threads: 8
    version: 
        config['gatkVersion']
    shell: """
module load GATK/{version}
java -Xmx4g -jar $GATKJAR \
-T PrintReads \
-nct {threads} \
-I {input.bam} \
-R {input.reference} \
-BQSR {input.recal_table} \
-o {output}
"""

rule gatk_base_recalibrator:
    input:
        bam = "{base}.bam",
        bai = "{base}.bam.bai",
        dbsnp = config['resources']['dbsnp'],
        reference = config['references'][config['reference']]
    output:
        "{base}.bam.recal_table"
    params:
        batch = "-l nodes=1:gpfs -q ccr"
    threads:
        8
    version: config['gatkVersion']
    shell: """
module load GATK/{version}
java -Xmx4g -jar $GATKJAR \
-T BaseRecalibrator \
-nct {threads} \
-I {input.bam} \
-R {input.reference} \
-knownSites {input.dbsnp} \
-o {output}
"""

rule gatk_realigner_target_creator:
    input:
        bam = "{base}.bam",
        bai = "{base}.bam.bai",
        known = config['resources']['knownIndels'],
        reference = config['references'][config['reference']]
    output: 
        "{base}.bam.intervals"
    params:
        batch = "-l nodes=1:gpfs"
    threads:
        16
    version: config['gatkVersion']
    run:
        knownIndels = _knownIndels_to_GATK(input.known)
        shell("""
module load GATK/{version}
java -Xmx4g -jar $GATKJAR \
-T RealignerTargetCreator \
-I {input.bam} \
-R {input.reference} \
{knownIndels} \
-nt {threads} \
-o {output}
""")

rule gatk_indel_realigner:
    input:
        bam = "{base}.bam",
        bai = "{base}.bam.bai",
        known = config['resources']['knownIndels'],
        intervals = "{base}.bam.intervals",
        reference = config['references'][config['reference']]
    output:
        "{base}.realigned.bam"
    params:
        batch = "-l nodes=1:gpfs"
    threads: 8
    version: config['gatkVersion']
    run:
        knownIndels = _knownIndels_to_GATK(input.known)
        shell("""
module load GATK/{version}
java -Xmx4g -jar $GATKJAR \
-T IndelRealigner \
-I {input.bam} \
-R {input.reference} \
{knownIndels} \
--targetIntervals {input.intervals} \
-o {output}
""")

rule index_bam:
    threads: 8
    input: 
        "{base}.bam"
    output: 
        "{base}.bam.bai"
    params: batch="-l nodes=1:gpfs"
    version: "0.4.6"
    shell: """
module load sambamba/{version}
sambamba index -t {threads} {input}
"""


rule markdups:
    threads: 1
    input: "{base}.bam",
           "{base}.bam.bai"
    output: bam="{base}.md.bam",
            metrics="{base}.bam.dupmetrics"
    params: 
        batch="-l nodes=1:gpfs"
    version: "1.119"
    shell: """
module load picard/{version}
java -Xmx22g -jar $PICARDJARPATH/MarkDuplicates.jar \
AS=true M={output.metrics} \
O={output.bam} \
I={input[0]} \
REMOVE_DUPLICATES=false \
VALIDATION_STRINGENCY=SILENT
"""

rule mergeBam2Sample:
    params:
        batch="-l nodes=1:gpfs"
    input: _get_unit_bams_from_sample
    output:
        "RESULTS/{reference,\w+}.{aligner,\w+}/{sample,\w+}/{sample}.bam"
    threads: 2
    version: "1.119"
    run:
        print(wildcards)
        cmd = """module load picard/1.119 && java -jar $PICARDJARPATH/MergeSamFiles.jar AS=true USE_THREADING=true VALIDATION_STRINGENCY=SILENT {0}""".format(' '.join(['I={0}'.format(i) for i in input]))
        cmd = cmd + " O={output}"
        print(cmd)
        shell(cmd,shell=True)

