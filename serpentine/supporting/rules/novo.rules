## novoalign

rule novoalign:
    """Map individual units with novoalign

    This rule uses the reference genome, after index creation,
    and maps the reads. Read group info is added automatically
    (LB, SM, PL, and ID).  Output is a coordinate-sorted BAM file.
    The output file is marked as temp().
    """
    input:
        index=lambda wildcards: SERPENTINE_HOME+"/resources/mapping/novoindex_"+ config['novoVersion'] +"/"+config['reference']+".index",
        fastq=lambda wildcards: config["unit_fastq"][wildcards.unit]
    output:
        temp("RESULTS/{reference}.novo/{sample}/{unit,\w+}.unit.bam")
    version: config['novoVersion']
    params:
        batch ="-l nodes=1:gpfs -q ccr",
        output_prefix = "RESULTS/{reference}.novo/{sample}/{unit,\w+}.unit",
        sample =lambda wildcards: UNIT_TO_SAMPLE[wildcards.unit],
        library=lambda wildcards: UNIT_TO_LIBRARY[wildcards.unit]
    log: "RESULTS/{reference}.novo/{sample}/{unit}.novo.log"
    threads: 64
    shell: """
module load novocraft/{version}
novoalign -d {input.index} \
-f {input.fastq} \
-a -k -o Sync \
-c {threads} -o SAM $'@RG\tID:{wildcards.unit}\tPU:{wildcards.unit}\tLB:{params.library}\tSM:{params.sample}\tPL:Illumina'\
| samtools view -Sbh - \
| samtools sort -m 30000000000 - {params.output_prefix}
"""


rule novo_index_fasta:
    """Index fasta input for novo.

    This rule uses config['references'][config['reference']] to get a specific fasta 
    file for indexing. The index is created prior to mapping anything.
    """
    input: config['references'][config['reference']]
    output: SERPENTINE_HOME + "/resources/mapping/novoindex_" + config['novoVersion'] + "/{reference}.index"
    log: SERPENTINE_HOME + "/resources/mapping/novoindex_" + config['novoVersion'] + "/{reference}.log"
    version: config['novoVersion']
    params: batch='-l nodes=1:gpfs'
    shell: """
module load novocraft/{version}
novoindex -m {output} {input}
"""



